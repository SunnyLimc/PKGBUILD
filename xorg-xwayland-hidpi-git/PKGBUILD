pkgname=xorg-xwayland-hidpi-git
pkgver=21.1.99.1.r17918.395c25f18
pkgrel=1
license=('custom')
pkgdesc='run X clients under wayland (git ver, hidpi patch)'
url='https://gitlab.freedesktop.org/xorg/xserver'
arch=('x86_64')
groups=('xorg')
provides=("xorg-xwayland=${pkgver/.r*/}" "xorg-xwayland-git")
conflicts=("xorg-xwayland" "xorg-xwayland-git")
depends=('nettle' 'libegl' 'libepoxy' 'systemd-libs' 'libxfont2'
  'pixman' 'xorg-server-common' 'libxcvt'
)
makedepends=('meson' 'xorgproto' 'xtrans' 'libxkbfile' 'dbus'
  'xorg-font-util'
  'wayland' 'wayland-protocols'
  'libdrm' 'mesa-libgl'
  'systemd'
  'egl-wayland'
)
source=(
  "${pkgname}::git+${url}.git"
  "https://raw.githubusercontent.com/SunnyLimc/PKGBUILD/main/xorg-xwayland-hidpi-git/xwayland-hidpi.patch"
)
sha256sums=(
  'SKIP'
  '8cc556c4a68d354d19e11b5292e35fb41172bf08873a47a34493898163920709'
)
pkgver() {
  cd "${pkgname}"
  set -o pipefail
  grep -zoP "project\([^)]*\)" meson.build | xargs --null echo | perl -ne '/\sversion:\s'"'"'([0-9.]*).*'"'"'/ and print $1'
  printf ".r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname}"
  patch -N -p1 <../xwayland-hidpi.patch
}

build() {
  # dont build other things
  # add default parameters as the official PKGBUILD
  # remove xkb_path setting due to the bug of missing config in runtime
  arch-meson "${pkgname}" build \
    -D xorg=false \
    -D xwayland=true \
    -D xvfb=false \
    -D xnest=false \
    -D xquartz=false \
    -D xwin=false \
    -D docs=false \
    -D xdmcp=false \
    -D xcsecurity=true \
    -D ipv6=true \
    -D dri3=true \
    -D xwayland_eglstream=true \
    -D glamor=true \
    -D xkb_output_dir=/var/lib/xkb
  # print config
  meson configure build
  ninja -C build
}

package() {
  # bin + manpage + .pc file
  install -m755 -Dt "${pkgdir}"/usr/bin build/hw/xwayland/Xwayland
  install -m644 -Dt "${pkgdir}"/usr/share/man/man1 build/hw/xwayland/Xwayland.1
  install -m644 -Dt "${pkgdir}"/usr/lib/pkgconfig build/hw/xwayland/xwayland.pc
  # license
  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" "${pkgname}"/COPYING
}
