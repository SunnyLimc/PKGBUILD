pkgbase=ttf-limc-collection
pkgname=(ttf-{windows,firacode,firacode-nf,lxgw-wenkai-screen,lxgw-cleargothic-book,sarasa-gothic})
pkgver=0.0.1
pkgrel=1
license=('custom')
pkgdesc='A font collection made by limc and aims to provide zh-* users a better experience'
url='https://github.com/SunnyLimc/PKGBUILD/tree/main/ttf-limc-collection'
arch=('any')
provides=('ttf-limc-collection')
makedepends=(
  'unzip'
  'p7zip'
)
source=(
  'FiraCode.zip::https://github.com/tonsky/FiraCode/releases/download/6.2/Fira_Code_v6.2.zip'
  'FiraCodeNF.zip::https://github.com/ryanoasis/nerd-fonts/releases/download/v2.2.2/FiraCode.zip'
  'SarasaGothic.7z::https://github.com/be5invis/Sarasa-Gothic/releases/download/v0.37.4/sarasa-gothic-ttc-0.37.4.7z'
  'https://github.com/lxgw/LxgwWenKai-Screen/releases/download/v1.245/LXGWWenKaiScreen.ttf'
  'https://github.com/lxgw/LxgwWenKai-Screen/releases/download/v1.245/LXGWWenKaiScreenR.ttf'
  'https://github.com/lxgw/LxgwNewClearGothic/releases/download/v0.235/LXGWNewClearGothic-Book.ttf'
)
noextract=(
  'FiraCode.zip'
  'FiraCodeNF.zip'
  'SarasaGothic.7z'
)
sha256sums=(
  '0949915ba8eb24d89fd93d10a7ff623f42830d7c5ffc3ecbf960e4ecad3e3e79'
  '20182e6e7c42cf8ab479d83af3200266261ec9bd4e80cdaceb793ecd56c9a398'
  'd5cde19772923d07d498156762631936e54484f94e68403eb4517b4219a3288a'
  'c5abd3121a9dad6afefed47aed17ac6229444ee9b22445c17006846addc0dc2a'
  '89e5a74a677c8ad990193a90f9aee4762cf8eafe9980b6aef380291faf55b4ea'
  '6128153f595f18cf15f97b99b008d60c957712ac6fdbff6e91e64d7845741963'
)

pkgver() {
  printf "r%s" "$(git rev-list --count HEAD)"
}

#! put your ttf/ttc file extract from windows DIRECTLY
#! (without wrap by any folder) in a zip archive named 'windows_fonts.zip'
#! then put the zip to the '$pkgsrc' (if you havnt set it, usually is the "src/" subfolder of PKGBUILD)
package_ttf-windows() {
  [[ ! -f "windows_fonts.zip" ]] && return 0
  mkdir -p "$pkgname"
  unzip -q -o 'windows_fonts.zip' -d "$pkgname"
  local fonts
  mapfile -t fonts < <(ls -d "$pkgname"/*.ttf)
  mapfile -O ${#fonts[@]} -t fonts < <(ls -d "$pkgname"/*.ttc)
  [[ -z ${#fonts[@]} ]] && return 0
  provides+=(emoji-font ttf-tahoma ttf-ms-fonts)
  conflicts+=(ttf-tahoma ttf-ms-fonts)
  for ((i = 0; i < ${#fonts[@]}; i++)); do
    install -Dm644 "${fonts[$i]}" -t "$pkgdir/usr/share/fonts/TTF"
  done
}

package_ttf-firacode() {
  mkdir -p "$pkgname"
  unzip -q -o 'FiraCode.zip' -d "$pkgname"
  local fonts
  mapfile -t fonts < <(ls -d "$pkgname"/ttf/*)
  for ((i = 0; i < ${#fonts[@]}; i++)); do
    install -Dm644 "${fonts[$i]}" -t "$pkgdir/usr/share/fonts/TTF"
  done
}

package_ttf-firacode-nf() {
  mkdir -p "$pkgname"
  unzip -q -o 'FiraCodeNF.zip' -d "$pkgname"
  local fonts
  mapfile -t fonts < <(ls -d "$pkgname"/*Mono.ttf)
  mapfile -O ${#fonts[@]} -t fonts < <(ls -d "$pkgname"/*Complete.ttf)
  for ((i = 0; i < ${#fonts[@]}; i++)); do
    install -Dm644 "${fonts[$i]}" -t "$pkgdir/usr/share/fonts/TTF"
  done
  install -Dm644 "$pkgname/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_ttf-lxgw-wenkai-screen() {
  mkdir -p "$pkgname"
  local fonts=('LXGWWenKaiScreen.ttf' 'LXGWWenKaiScreenR.ttf')
  for ((i = 0; i < ${#fonts[@]}; i++)); do
    install -Dm644 "${fonts[$i]}" -t "$pkgdir/usr/share/fonts/TTF"
  done
}

package_ttf-lxgw-cleargothic-book() {
  mkdir -p "$pkgname"
  local fonts=('LXGWNewClearGothic-Book.ttf')
  for ((i = 0; i < ${#fonts[@]}; i++)); do
    install -Dm644 "${fonts[$i]}" -t "$pkgdir/usr/share/fonts/TTF"
  done
}

package_ttf-sarasa-gothic() {
  mkdir -p "$pkgname"
  # disable output(bso0) and progress output(bsp0) and extract(e)
  # with overwrite-mode(aoa) to folder(o) "$pkgname"
  7z -bso0 -bsp0 -aoa -o"$pkgname" e 'SarasaGothic.7z'
  local fonts
  mapfile -t fonts < <(ls -d "$pkgname"/*)
  for ((i = 0; i < ${#fonts[@]}; i++)); do
    install -Dm644 "${fonts[$i]}" -t "$pkgdir/usr/share/fonts/TTF"
  done
}
